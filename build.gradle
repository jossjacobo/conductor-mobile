plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.willowtreeapps'
version = '0.18.0'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.yaml:snakeyaml:1.19"
    compile "org.tinylog:tinylog:1.3.2"
    compile "io.appium:java-client:6.1.0"
    compile "org.glassfish:javax.annotation:10.0-b28"
    compile "org.junit.platform:junit-platform-surefire-provider:1.0.0-M3"
    compile "org.testng:testng:6.14.3"
    compile "org.assertj:assertj-swing-junit:3.4.0"
    compile "org.tinylog:tinylog:1.3.1"
    compile "com.saucelabs:sauce_testng:2.1.23"

    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "org.junit.jupiter:junit-jupiter-engine:5.0.0-M3"
    testCompile "org.junit.vintage:junit-vintage-engine:4.12.0-M3"
    testCompile "org.mockito:mockito-core:2.18.3"
}

test {
    useTestNG()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {

    repositories {
        maven {
            def isReleaseVersion = !(project.version =~ /-SNAPSHOT$/)
            if (isReleaseVersion) {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            } else {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            }

            credentials {
                username = project.findProperty('sonatype.username')
                password = project.findProperty('sonatype.password')
            }
        }
    }

    publications {
        lib(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            def siteUrl = 'https://github.com/willowtreeapps/conductor-mobile'
            def gitUrl = 'https://github.com/willowtreeapps/conductor-mobile.git'

            pom {
                name = project.name
                description = 'Conductor Mobile is a port of the Conductor Web Framework for iOS and Android'
                url = siteUrl

                scm {
                    url = siteUrl
                    connection = gitUrl
                    developerConnection = gitUrl
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'joss'
                        name = 'Jossay Jacobo'
                    }
                }
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
    signing {
        sign publishing.publications.lib
    }
}
