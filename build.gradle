plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id "com.github.ben-manes.versions" version "0.25.0"
}

group = 'com.willowtreeapps'
version = '0.20.1-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    isReleaseVersion = !(project.version =~ /-SNAPSHOT$/)

    snakeYamlVersion = '1.25'
    tinyLogVersion = '1.3.6'
    appiumJavaClientVersion = '7.3.0'
    glassfishJavaxAnnotationVersion = '10.0-b28'
    junitPlatformSurefireProviderVersion = '1.0.0-M3'
    testngVersion = '7.1.0'
    assertjSwingJunitVersion = '3.4.0'
    sauceTestngVersion = '2.1.25'

    hamcrestLibraryVersion = '1.3'
    junitJupiterEngineVersion = '5.0.0-M3'
    junitVintageEngineVersion = '4.12.0-M3'
    mockitoCoreVersion = '2.18.3'
}

dependencies {
    compile "org.tinylog:tinylog:$tinyLogVersion"
    compile "org.yaml:snakeyaml:$snakeYamlVersion"
    compile "io.appium:java-client:$appiumJavaClientVersion"
    compile "org.glassfish:javax.annotation:$glassfishJavaxAnnotationVersion"
    compile "org.junit.platform:junit-platform-surefire-provider:$junitPlatformSurefireProviderVersion"
    compile "org.testng:testng:$testngVersion"
    compile "org.assertj:assertj-swing-junit:$assertjSwingJunitVersion"
    compile "com.saucelabs:sauce_testng:$sauceTestngVersion"

    testCompile "org.hamcrest:hamcrest-library:$hamcrestLibraryVersion"
    testCompile "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
    testCompile "org.junit.vintage:junit-vintage-engine:$junitVintageEngineVersion"
    testCompile "org.mockito:mockito-core:$mockitoCoreVersion"
}


test {
    useTestNG()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {

    repositories {
        maven {
            if (isReleaseVersion) {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            } else {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            }

            credentials {
                username = project.findProperty('sonatype.username')
                password = project.findProperty('sonatype.password')
            }
        }
    }

    publications {
        lib(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            def siteUrl = 'https://github.com/willowtreeapps/conductor-mobile'
            def gitUrl = 'https://github.com/willowtreeapps/conductor-mobile.git'

            pom {
                name = project.name
                description = 'Conductor Mobile is a port of the Conductor Web Framework for iOS and Android'
                url = siteUrl

                scm {
                    url = siteUrl
                    connection = gitUrl
                    developerConnection = gitUrl
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'joss'
                        name = 'Jossay Jacobo'
                    }
                }
            }
        }
    }
}

if (project.hasProperty("signing.keyId") && isReleaseVersion) {
    signing {
        sign publishing.publications.lib
    }
}
